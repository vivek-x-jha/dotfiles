#!/usr/bin/env zsh

# Check for 'fileicon' binary
command -v fileicon &>/dev/null || {
  printf "${BRIGHTRED}%s %s${BRIGHTYELLOW}%s${RESET}\n" "✘" "'fileicon' dependency not found" " - please install or check in \$PATH."
  return 1
}

local icon_dir="${1:-$HOME/Pictures/icons}"
[[ -d $icon_dir ]] || {
  printf "${BRIGHTRED}%s %s${BRIGHTYELLOW}%s${RESET}\n" "✘" "Icon directory not found" " - ${icon_dir/#$HOME/~}"
  return 1
}

table_width=60

# Directory - Icons
dir_icons=(
  "/Applications/1Password.app" "$icon_dir/1password.png"
  "/Applications/Anki.app" "$icon_dir/anki.png"
  "/Applications/ChatGPT.app" "$icon_dir/chatgpt.png"
  "/Applications/Discord.app" "$icon_dir/discord.png"
  "/Applications/Figma.app" "$icon_dir/figma.png"
  "/Applications/Firefox.app" "$icon_dir/firefox.png"
  "/Applications/Hammerspoon.app" "$icon_dir/hammerspoon.png"
  "/Applications/iTerm.app" "$icon_dir/iterm2.png"
  "/Applications/iTermAI.app" "$icon_dir/ai-brain.png"
  "/Applications/KeyCastr.app" "$icon_dir/keycastr.png"
  "/Applications/Messenger.app" "$icon_dir/messenger.png"
  "/Applications/Neovim-iTerm.app" "$icon_dir/neovim-iterm.png"
  "/Applications/Neovim-Wezterm.app" "$icon_dir/neovim-wezterm.png"
  "/Applications/Notion Calendar.app" "$icon_dir/notion-calendar.png"
  "/Applications/Postman.app" "$icon_dir/postman.png"
  "/Applications/Skim.app" "$icon_dir/skim.png"
  "/Applications/Slack.app" "$icon_dir/slack.png"
  "/Applications/Spotify.app" "$icon_dir/spotify.png"
  "/Applications/Visual Studio Code.app" "$icon_dir/vscode.png"
  "/Applications/WeChat.app" "$icon_dir/wechat.png"
  "/Applications/WezTerm.app" "$icon_dir/wezterm.png"
  "/Applications/WhatsApp.app" "$icon_dir/whatsapp.png"
  "$HOME/Developer" "$icon_dir/developer.png"
  "$HOME/Downloads" "$icon_dir/download.png"
  "$HOME/Dropbox/content" "$icon_dir/content.png"
  "$icon_dir" "$icon_dir/png.png"
  "$HOME/Pictures/screenshots" "$icon_dir/screenshot.png"
  "$HOME/Pictures/wallpapers" "$icon_dir/wallpaper.png"
  "$HOME/.dotfiles" "$icon_dir/gear.png"
)

print_separator() {
  printf "${BRIGHTBLACK}%-${table_width}s${RESET}\n" | tr ' ' '-'
}

# Create header
print_separator
printf "%-2s ${BLUE}%-37s ${MAGENTA}%-5s${RESET}\n" '' 'App / Directory' "${icon_dir/#$HOME/~}"
print_separator

# Create rows
local success=0 fail=0

for ((i = 1; i < ${#dir_icons[@]}; i += 2)); do
  local dir="$dir_icons[i]"
  local icon="$dir_icons[i + 1]"

  print_status() {
    local highlight="${1}%-1s ${WHITE}%-37s ${BRIGHTMAGENTA}%-5s${RESET}"
    local status_icon="$2"

    printf "$highlight\n" "$status_icon" "${dir/#$HOME/~}" "${icon:t}"
  }

  fileicon set "$dir" "$icon" &>/dev/null || {
    print_status "$RED" "✘"
    ((fail++))
    continue
  }

  print_status "$GREEN" "✔"
  ((success++))
done

# Create Footer
print_separator
printf "${GREEN}%-2s ${BRIGHTBLACK}%-37s${RESET}\n" "$success" 'Folder icon(s) updated successfully'
((fail)) && printf "${RED}%-2s ${BRIGHTBLACK}%-37s${RESET}\n" "$fail" 'Folder icon(s) failed to update'

return 0
