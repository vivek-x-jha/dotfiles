#!/usr/bin/env zsh

# Ensure tmux available
command -v tmux &>/dev/null || {
  printf 'tmux not available\n' >&2
  return 1
}

local window_label='main'
local left_label='edit'
local top_right_label='ai'
local bottom_right_label='test'

local left_cmd='nvim'
local top_right_cmd='codex'
local bottom_right_cmd='clear;'

local root=''

# Doc helper
usage() {
  cat <<'EOF'
Usage: work <options> <session>

Options:
  -h, --help                     Show this help message and exit
  -r, --reload                   Kill current tmux server and start a fresh session
  -p, --python                   Initialize Python project (runs 'uvi' if no pyproject.toml)

Layout customization:
  --window-label NAME            Set the tmux window name (default: main)
  --left-label NAME              Pane 1 (left) label (default: edit)
  --top-right-label NAME         Pane 2 (top-right) label (default: ai)
  --bottom-right-label NAME      Pane 3 (bottom-right) label (default: test)

Command customization:
  --left-cmd CMD                 Command for left pane (default: nvim)
  --top-right-cmd CMD            Command for top-right pane (default: codex)
  --bottom-right-cmd CMD         Command for bottom-right pane (default: 'clear;')

Examples:
  work                           Attach or create default session
  work myapp                     Open session in ~/developer/myapp
  work --window-label dev myapp  Custom window name
  work -r                        Reset tmux server (tmux kill-server && tmux new-session)
EOF
}

# Handle <options> for different project setups
while [[ $# -gt 0 ]]; do
  case "$1" in
  -h | --help)
    usage
    return
    ;;

  -r | --reload)
    [[ -n $TMUX ]] && {
      printf 'Stopping tmux server... rerun `work -r` once tmux exits to start fresh.\n'
      tmux kill-server &>/dev/null
      return
    }

    tmux list-sessions &>/dev/null && tmux kill-server &>/dev/null

    # Wait up to 1-2 seconds after tmux klls server to create/resurrect
    local attempt=0
    while ((attempt < 3)); do
      TMUX='' tmux new-session && return
      sleep 0.5
      attempt=$((attempt + 1))
    done

    printf 'work: failed to start new tmux session after reload.\n' >&2
    return 1
    ;;

  -p | --python)
    bottom_right_cmd+=' if test ! -f pyproject.toml; then uvi; fi'
    shift
    ;;

  --window-label)
    window_label="$2"
    shift 2
    ;;

  --left-label)
    left_label="$2"
    shift 2
    ;;

  --top-right-label)
    top_right_label="$2"
    shift 2
    ;;

  --bottom-right-label)
    bottom_right_label="$2"
    shift 2
    ;;

  --left-cmd)
    left_cmd="$2"
    shift 2
    ;;

  --top-right-cmd)
    top_right_cmd="$2"
    shift 2
    ;;

  --bottom-right-cmd)
    bottom_right_cmd="$2"
    shift 2
    ;;

  --) shift && break ;;
  *) break ;;
  esac
done

# Handle <sessions> for different project setups
local session="$1"
local should_prompt_description=0

case "$session" in
'')
  # If run without args inside TMUX session -> mutate to `tmux list-sessions`
  if [[ -n $TMUX ]]; then
    tmux list-sessions
    return $?
  fi

  # If run without args outside TMUX session -> mutate to `tmux attach-session or new-session`
  tmux attach-session || tmux new-session
  return $?
  ;;

# Default arg behavior & specific cases
nvim | wezterm) root="$HOME/.dotfiles" ;;
*) root="$HOME/developer/$session" && should_prompt_description=1 ;;
esac

# Helper func: respects whether current shell is already inside tmux
swatt() {
  local target="$1"
  if [[ -n $TMUX ]]; then
    tmux switch-client -t "$target" 2>/dev/null || tmux switch-client -l
  else
    tmux attach-session -t "$target" 2>/dev/null || tmux attach-session
  fi
}

# Short circuit if session already exists
tmux has-session -t "$session" &>/dev/null && {
  swatt "$session"
  return $?
}

local description=''

# Update agent initial cmd with description for easy conversation history filtering
((should_prompt_description)) && {
  local prompt
  printf -v prompt '%bEnter description? %b' "$BRIGHTYELLOW" "$RESET"
  [[ -t 0 ]] && read -r "?$prompt"
  description="${REPLY:+: $REPLY}"
  top_right_cmd+=" '$session$description'"
}

# General template creator
# TODO expand for different templates
tmux_template() {
  local left_id="$session:$window_label.1"
  local top_right_id="$session:$window_label.2"
  local bottom_right_id="$session:$window_label.3"

  # Setup project structure
  mkdir -p "$root/prompts"

  # Initialize session with given configuration
  tmux new-session -d -s "$session" -c "$root" -n "$window_label"

  # Configure window layout
  tmux split-window -h -t "$left_id" -c "$root"
  tmux split-window -v -t "$top_right_id" -c "$root"

  # Label panes
  tmux select-pane -t "$left_id" -T "$left_label"
  tmux select-pane -t "$top_right_id" -T "$top_right_label"
  tmux select-pane -t "$bottom_right_id" -T "$bottom_right_label"

  # Execute startup commands
  tmux send-keys -t "$left_id" -l -- "$left_cmd"
  tmux send-keys -t "$left_id" C-m
  tmux send-keys -t "$top_right_id" -l -- "$top_right_cmd"
  tmux send-keys -t "$top_right_id" C-m
  tmux send-keys -t "$bottom_right_id" -l -- "$bottom_right_cmd"
  tmux send-keys -t "$bottom_right_id" C-m

  tmux select-pane -t "$bottom_right_id"
}

tmux_template || {
  printf 'work: failed to scaffold session %s\n' "$session" >&2
  usage
  return 1
}

swatt "$session"
return $?
